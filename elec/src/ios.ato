import USBCConn from "usb-connectors/usb-connectors.ato"
from "generics/interfaces.ato" import Power, USB2, I2C, Pair, DiffPair
from "generics/buttons.ato" import ButtonPullup, NoButton
import SK6805EC20 from "sk6805-ec20/elec/src/sk6805-ec20.ato"
import DRV5053RAQDBZR from "drv5053raqdbzr/elec/src/drv5053raqdbzr.ato"
import DRV2605 from "drv2605ldgsr/elec/src/drv2605ldgsr.ato"


module RightAngleButtonPullup from ButtonPullup:
    btn -> TS_1093C_A7B2_D1

module IOs:
    """
    Contains all the IOs of the board:
    - USBC input connector - provides 5V to the rest of the board
    - 3 SK6805EC20 LEDs for status indication (charging, battery level, signal)
    - Buttons for menu and power
    - Hall effect sensor for trigger angle detection
    """
    # top level interfaces
    power3v3 = new Power
    power5v = new Power
    usb2 = new USB2
    i2c = new I2C
    led_din = new Pair
    hall_sense = new Pair
    pot_output = new Pair
    power_btn = new Pair
    menu_btn = new Pair


    # Submodules
    usbc_connector = new USBCConn
    led1 = new SK6805EC20
    led2 = new SK6805EC20
    led3 = new SK6805EC20
    led4 = new SK6805EC20
    power_button = new RightAngleButtonPullup
    menu_button = new RightAngleButtonPullup
    hall_effect = new DRV5053RAQDBZR
    haptic_driver = new DRV2605
    haptic_connector = new DB125_2_54_2P_GN


    # Connect power
    power5v ~ usbc_connector.power
    power3v3 ~ led1.power
    power3v3 ~ led2.power
    power3v3 ~ led3.power
    power3v3 ~ led4.power
    power3v3 ~ power_button.power
    power3v3 ~ menu_button.power
    power3v3 ~ hall_effect.power
    power3v3 ~ haptic_driver.power


    # Connect the LEDs
    led_din ~ led1.din
    led1.dout ~ led2.din
    led2.dout ~ led3.din
    led3.dout ~ led4.din


    # Connect the I2C bus
    i2c ~ haptic_driver.i2c


    # Connect the buttons
    power_btn ~ power_button.output
    menu_btn ~ menu_button.output


    # Connect Devices
    usb2 ~ usbc_connector.usb2
    hall_sense ~ hall_effect.output


    # Connect the haptic driver
    haptic_driver.output ~ haptic_connector.input


component DB125_2_54_2P_GN:
    # component DB125-2.54-2P-GN
    footprint = "CONN-TH_DB125-2.54-2P-GN"
    lcsc_id = "C918120"
    mpn = "C918120"
    # pins

    input = new DiffPair
    input.p ~ pin 1
    input.n ~ pin 2

module TS_1093C_A7B2_D1 from NoButton:
    btn = new _TS_1093C_A7B2_D1
    in ~ btn.in
    out ~ btn.out

component _TS_1093C_A7B2_D1:
    # component TS-1093C-A7B2-D1
    footprint = "SW-TH_TS-1093C-A7B3-D1"
    lcsc_id = "C2682918"
    mpn = "C2682918"
    # pins
    signal in ~ pin 1
    signal out ~ pin 2

