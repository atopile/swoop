from "generics/interfaces.ato" import Pair, Power
import BatteryJST from "batteries/batteries.ato"
import AMS111733 from "ams1117-33/elec/src/ams1117-33.ato"
import TPS63020DSJR from "tps63020dsjr/elec/src/tps63020dsjr.ato"
import BQ24040DSQR from "bq24045dsqr/elec/src/bq24045dsqr.ato"
import Diode from "generics/diodes.ato"
from "generics/vdivs.ato" import VDivLowPassFilter, VDiv
from "generics/debug.ato" import TestPoint

module PowerSupply:
    power5v = new Power
    power3v3 = new Power
    battery_voltage_sense = new Pair
    charge_status = new Pair

    battery = new Battery
    charger = new Charger
    buckboost = new BuckBoost
    ldo = new LDO
    battery_voltage_sensor = new VDivLowPassFilter


    # Configure the battery voltage sense
    battery_voltage_sensor.v_in = 4.2V +/- 1%
    battery_voltage_sensor.v_out = 3V +/- 1V
    battery_voltage_sensor.cutoff_frequency = 100Hz +/- 20%

    charger.bq2404x.charge_current = 500mA +/- 10%

    buckboost.tps_buck_boost.v_in = 2.5V to 4.2V
    buckboost.tps_buck_boost.v_out = 3.6V to 3.9V
    buckboost.tps_buck_boost.i_q = 1uA to 5uA

    power5v ~ charger.power_in
    charger.power_batt ~ battery.power
    battery.power ~ battery_voltage_sensor.power
    charger.bq2404x.charge_status ~ charge_status

    buckboost.power_in ~ battery.power
    buckboost.power_out ~ ldo.power_in
    ldo.power_out ~ power3v3

    # add test points
    power5v_tp = new TestPoint
    power3v3_tp = new TestPoint
    battery_tp = new TestPoint
    charger_tp = new TestPoint

    power5v_tp.point ~ power5v.vcc
    power3v3_tp.point ~ power3v3.vcc
    battery_tp.point ~ battery.power.vcc
    charger_tp.point ~ charger.bq2404x.charge_status.io


module DiodeOrUSB:
    """
    This module is used to select between the USB power and the battery power.
    USB power is diode or'd with the battery power, there is no diode between the
    battery and the output for efficiency.
    """
    power_usb = new Power
    power_batt = new Power
    power_out = new Power

    diode = new Diode
    power_usb.vcc ~ diode.anode
    diode.cathode ~ power_out.vcc
    power_batt ~ power_out

    diode -> SS14

module Battery:
    power = new Power

    # Battery connector
    batt_jst = new BatteryJST
    power ~ batt_jst.power

module Charger:
    power_in = new Power
    power_batt = new Power

    bq2404x = new BQ24040DSQR
    power_in ~ bq2404x.power_in
    power_batt ~ bq2404x.power_batt

module BuckBoost:
    power_in = new Power
    power_out = new Power

    # 2A output buck-boost
    tps_buck_boost = new TPS63020DSJR
    power_in ~ tps_buck_boost.power_in
    power_out ~ tps_buck_boost.power_out

module LDO:
    power_in = new Power
    power_out = new Power

    # 1A fixed 3v3 LDO
    ams1117_33 = new AMS111733
    power_in ~ ams1117_33.power_in
    power_out ~ ams1117_33.power_out

component SS14 from Diode:
    # component SS14
    footprint = "SMA_L4.2-W2.6-LS5.3-RD"
    lcsc_id = "C2480"
    mpn = "C2480"
    # pins
    anode ~ pin 2
    cathode ~ pin 1